apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"


    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    maven {
        url "https://dl.bintray.com/navitiasdkteam/NavitiaSDK"
    }
}

dependencies {
    compile 'org.kisio.sdk:navitia-sdk:0.2.0'

    // Litho
    compile 'com.facebook.litho:litho-core:0.6.1'
    compile 'com.facebook.litho:litho-widget:0.6.1'
    provided 'com.facebook.litho:litho-annotations:0.6.1'

    annotationProcessor 'com.facebook.litho:litho-processor:0.6.1'

    // SoLoader
    compile 'com.facebook.soloader:soloader:0.2.0'

    implementation 'com.android.support:appcompat-v7:26.0.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.0'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.0'
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

def pomConfig = {
    licenses {
        license {
            name "GNU General Public License, Version 3"
            url "https://www.navitia.io/api-term-of-use"
            distribution "repo"
        }
    }

    developers {
        developer {
            name "Kisio Digital SDK"
            email "team.sdk@kisio.org"
            organization "Kisio Digital"
            organizationUrl "http://www.kisiodigital.com/"
        }
    }

    scm {
        connection "scm:git:git://github.com/CanalTP/NavitiaSDKUX_android.git"
        developerConnection "scm:git:ssh://github.com:CanalTP/NavitiaSDKUX_android.git"
        url "https://github.com/CanalTP/NavitiaSDKUX_android/tree/master"
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'org.kisio.sdk'
            artifactId 'navitia-sdk-ux'
            version '0.2.0'
            // Or use same version as in android branch
            // version = android.defaultConfig.versionName

            artifact bundleRelease
            artifact androidJavadocsJar
            artifact androidSourcesJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'NavitiaSDKUX android 0.2.0')
                root.appendNode('name', 'navitia-sdk-ux')
                root.appendNode('url', 'https://github.com/CanalTP/NavitiaSDKUX_android')

                def dependenciesNode = root.appendNode('dependencies')

                // List all compile dependencies and write to POM
                configurations.compile.getAllDependencies().each { Dependency dep ->
                    if (dep.group == null || dep.version == null || dep.name == null || dep.name == "unspecified")
                        return // ignore invalid dependencies

                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dep.group)
                    dependencyNode.appendNode('artifactId', dep.name)
                    dependencyNode.appendNode('version', dep.version)

                    if (!dep.transitive) {
                        // If this dependency is transitive, we should force exclude all its dependencies them from the POM
                        def exclusionNode = dependencyNode.appendNode('exclusions').appendNode('exclusion')
                        exclusionNode.appendNode('groupId', '*')
                        exclusionNode.appendNode('artifactId', '*')
                    } else if (!dep.properties.excludeRules.empty) {
                        // Otherwise add specified exclude rules
                        def exclusionsNode = dependencyNode.appendNode('exclusions')
                        dep.properties.excludeRules.each { ExcludeRule rule ->
                            def exclusionNode = exclusionsNode.appendNode('exclusion')
                            exclusionNode.appendNode('groupId', rule.group ?: '*')
                            exclusionNode.appendNode('artifactId', rule.module ?: '*')
                        }
                    }
                }

                root.children().last() + pomConfig
            }
        }
    }
}

// "gradle -Puser=someuser -Pkey=ASDFASDFASDF androidSourcesJar androidJavadocsJar bundleRelease bintrayUpload" to build and send to jcenter
bintray {
    user = project.hasProperty('user') ? property('user') : ''
    key = project.hasProperty('key') ? property('key') : ''
    publications = ['maven']
    dryRun = false //[Default: false] Whether to run this as dry-run, without deploying
    publish = false //[Default: false] Whether version should be auto published after an upload
    pkg {
        repo = 'NavitiaSDK'
        name = 'navitia-sdk-ux'
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/CanalTP/NavitiaSDKUX_android.git'
        version {
            name = '0.2.0'
            desc = 'NavitiaSDKUX android 0.2.0'
            released  = new Date()
            vcsTag = '0.2.0'
            gpg {
                sign = true
            }
        }
    }
}